-- Ansi C, following Kernighan & Ritchie (Second Edition).
-- (c) Ulf Persson 2003
-- part of BSc thesis at Chalmers University of Technology
--
-- NOTICE: the C program may need to be preprocessed by cpp. Also types defined
-- by typedefs have to be eliminated (these are known problems with the 
-- BNF grammar of Ansi C).

entrypoints Program ;

Progr.  Program ::= [Declaration] ;

(:[]).    [Declaration] ::= Declaration;
(:).      [Declaration] ::= Declaration [Declaration];

ProcDecl.           Declaration ::= "proc" Declarator Compound_stm ;
VarDecl.            Declaration ::= Type_specifier [Init_declarator] ";" ;


(:[]).   [Init_declarator] ::= Init_declarator ;
(:).     [Init_declarator] ::= Init_declarator "," [Init_declarator] ;

OnlyDecl.  Init_declarator ::= Ident ;
InitDecl.  Init_declarator ::= Ident "=" Initializer;

InitExpr.    Initializer ::= Exp2 ;

Tvoid.        Type_specifier ::= "void";
Tint.         Type_specifier ::= "int";
Tbool.        Type_specifier ::= "bool";

FuncIdent.  Declarator ::= Ident "(" Parameter_declarations ")" ;

ParamDec.     Parameter_declarations ::= Parameter_declaration ;                   
MoreParamDec. Parameter_declarations ::= Parameter_declarations "," Parameter_declaration ;

TypeAndParam.  Parameter_declaration ::= Type_specifier Ident ;

(:[]).   [Ident] ::= Ident ;
(:).     [Ident] ::= Ident "," [Ident] ;

CompStm.         Stm ::= Compound_stm ;
ExprStm.         Stm ::= Expression_stm ;
SelecStm.        Stm ::= Selection_stm ;
IterStm.         Stm ::= Iter_stm ;
JumpStm.         Stm ::= Jump_stm ;
PrintStm.        Stm ::= Print_stm ;

ScompOne.   Compound_stm ::= "{" "}" ;
ScompTwo.   Compound_stm ::= "{" [Stm] "}" ;
ScompThree. Compound_stm ::= "{" [Declaration] "}" ;
ScompFour.  Compound_stm ::= "{" [Declaration] [Stm] "}" ;

SexprOne.   Expression_stm ::= ";" ;
SexprTwo.   Expression_stm ::= Exp ";" ;

SselOne.    Selection_stm ::= "if" "(" Exp ")" Compound_stm ;
SselTwo.    Selection_stm ::= "if" "(" Exp ")" Compound_stm "else" Compound_stm ;

SiterOne.   Iter_stm ::= "while" "(" Exp ")" Compound_stm;
SiterTwo.   Iter_stm ::= "do" Compound_stm "while" "(" Exp ")" ";" ;
SiterThree. Iter_stm ::= "for" "(" Expression_stm Expression_stm ")" Compound_stm ;
SiterFour.  Iter_stm ::= "for" "(" Expression_stm Expression_stm Exp ")" Compound_stm;

SjumpTwo.   Jump_stm ::= "continue" ";" ;
SjumpThree. Jump_stm ::= "break" ";" ;
SjumpFour.  Jump_stm ::= "return" ";" ;
SjumpFive.  Jump_stm ::= "return" Exp ";" ;

Sprint.     Print_stm ::= "print(" [Exp1] ")" ";" ;

(:[]).   [Stm] ::= Stm ;
(:).     [Stm] ::= Stm [Stm];

Eassign.     Exp1  ::= Exp8 Assignment_op Exp1;
Elor.        Exp2  ::= Exp2 "||" Exp4;
Eland.       Exp3  ::= Exp3 "&&" Exp4;
Eeq.         Exp4  ::= Exp4 "==" Exp5;
Eneq.        Exp4  ::= Exp4 "!=" Exp5;
Elthen.      Exp5 ::= Exp5 "<" Exp6;
Egrthen.     Exp5 ::= Exp5 ">" Exp6;
Ele.         Exp5 ::= Exp5 "<=" Exp6;
Ege.         Exp5 ::= Exp5 ">=" Exp6;
Eplus.       Exp6 ::= Exp6 "+" Exp7;
Eminus.      Exp6 ::= Exp6 "-" Exp7;
Etimes.      Exp7 ::= Exp7 "*" Exp8;
Ediv.        Exp7 ::= Exp7 "/" Exp8;
Emod.        Exp7 ::= Exp7 "%" Exp8;
Epreinc.     Exp8 ::= "++" Exp8;
Epredec.     Exp8 ::= "--" Exp8;
Epreop.      Exp8 ::= Unary_operator Exp8;
Efunk.       Exp9 ::= Exp9 "(" ")";
Efunkpar.    Exp9 ::= Exp9 "(" [Exp1] ")";
Epostinc.    Exp9 ::= Exp9 "++";
Epostdec.    Exp9 ::= Exp9 "--";
Evar.        Exp10 ::= Ident;
Econst.      Exp10 ::= Constant;

Eint.          Constant ::= Integer;
Ebool.         Constant ::= Boolean;

Vtrue.         Boolean ::= "true";
Vfalse.        Boolean ::= "false";

Especial. Constant_expression ::= Exp3;

_. Exp   ::= Exp2 ;
_. Exp2  ::= Exp3 ;
_. Exp3  ::= Exp4 ;
_. Exp4  ::= Exp5 ;
_. Exp5  ::= Exp6 ;
_. Exp6  ::= Exp7 ;
_. Exp7  ::= Exp8 ;
_. Exp8  ::= Exp9 ;
_. Exp9  ::= Exp10 ;
_. Exp10 ::= Exp11 ;
_. Exp11 ::= Exp12 ;
_. Exp12 ::= Exp13 ;
_. Exp13 ::= Exp14 ;
_. Exp14 ::= Exp15 ;
_. Exp15 ::= Exp16 ;
_. Exp16 ::= Exp17 ;
_. Exp17 ::= "(" Exp ")" ;


Plus.        Unary_operator ::= "+" ;
Negative.    Unary_operator ::= "-" ;
Logicalneg.  Unary_operator ::= "!" ;

(:[]).   [Exp1] ::= Exp1 ;
(:).     [Exp1] ::= Exp1 "," [Exp1];

Assign.       Assignment_op ::= "=" ;
AssignMul.    Assignment_op ::= "*=" ;
AssignDiv.    Assignment_op ::= "/=" ;
AssignMod.    Assignment_op ::= "%=" ;
AssignAdd.    Assignment_op ::= "+=" ;
AssignSub.    Assignment_op ::= "-=" ;
AssignAnd.    Assignment_op ::= "&=" ;
AssignOr.     Assignment_op ::= "|=" ;

comment "/*" "*/" ;
comment "//";
comment "#";
